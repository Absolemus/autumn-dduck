#Использовать 1connector
#Использовать logos

#Область ОписаниеПеременных
// BSLLS:ExportVariables-off
&Деталька(Значение = "DDNS.Domain") 
Перем Домен Экспорт; // Домен, зарегистрированный в www.duckdns.org
&Деталька(Значение = "DDNS.Token") 
Перем Токен Экспорт; // Токен, полученный в www.duckdns.org
&Деталька(Значение = "DDNS.Delay", ЗначениеПоУмолчанию = 300000) 
Перем ЧастотаОбновления Экспорт; // Частота отправки сообщений в www.duckdns.org, в милисекундах
&Деталька(Значение = "DDNS.Use", ЗначениеПоУмолчанию = Ложь) 
Перем Использовать Экспорт; // Управление запуском обновления
&Деталька(Значение = "DDNS.ACMEtxt", ЗначениеПоУмолчанию = "") 
Перем ТекстДляСертификата Экспорт; // Управление запуском обновления
// BSLLS:ExportVariables-on
Перем URL; // Строка запроса к www.duckdns.org
Перем Лог; // Логирование
Перем ИндексРезультата; // Индекс результата в ответе
Перем ИндексIP; // Индекс IP-адреса в ответе
Перем ИндексИзменения; // Индекс изменения IP-адреса в ответе
#КонецОбласти

#Область ОбработчикиСобытий
&Желудь
Процедура ПриСозданииОбъекта()
	Лог.Отладка("Создание объекта ОбновляторDDNS");
КонецПроцедуры

&ФинальныйШтрих
Процедура УстановитьПараметрыАдреса() Экспорт 
	URL = СтрШаблон(
		"https://www.duckdns.org/update?domains=%1&token=%2&txt=%3&verbose=true", 
		Домен, 
		Токен, 
		ТекстДляСертификата
	);
	Лог.Отладка(СтрШаблон("Установлены параметры адреса: %1", URL));
КонецПроцедуры
#КонецОбласти

#Область ПрограммныйИнтерфейс
// Метод отправляет запрос на обновление IP-адреса в www.duckdns.org
// Для использования метода необходимо установить параметры Domain и Token в фалй autumn-properties.yml
Процедура Старт() Экспорт
	Пока Использовать() Цикл
		Попытка
			Ответ = КоннекторHTTP.Get(URL);
		Исключение
			Лог.Ошибка(СтрШаблон("В процессе отправки запроса произошла ошибка: %1", ОписаниеОшибки()));
		КонецПопытки;
		ОбработатьОтвет(Ответ);
		Приостановить(ЧастотаОбновления);
	КонецЦикла;
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура ОбработатьОтвет(Ответ)
	ОтветСервера = СтрРазделить(Ответ.Текст(), Символы.ПС);
	Если ОтветСервера.Количество() > ИндексРезультата Тогда
		Если ОтветСервера[ИндексРезультата] = "OK" Тогда
			Лог.Информация("Сообщение для обновления IP успешно отправлено");
			РасширеннаяИнформация(ОтветСервера);
		Иначе
			Лог.Ошибка("Попытка обновления IP не удалась");
		КонецЕсли;
	Иначе
		Лог.Ошибка("Сервер вернул пустой ответ");
	КонецЕсли;
КонецПроцедуры

Процедура РасширеннаяИнформация(ОтветСервера)
	Если ОтветСервера[ИндексИзменения] = "NOCHANGE" Тогда
		Лог.Информация("IP не изменился");
	Иначе
		Лог.Информация(СтрШаблон("IP изменился на %1", ОтветСервера[ИндексIP]));
	КонецЕсли;
КонецПроцедуры

Функция Использовать()
	Если Не ЗначениеЗаполнено(Домен)
		Или Не ЗначениеЗаполнено(Токен) Тогда
		Использовать = Ложь;
		Лог.Информация("В настройках не указаны параметры для обновления DDNS. Обновление не будет производиться.");
	КонецЕсли;

	Возврат Использовать;
КонецФункции
#КонецОбласти

#Область Инициализация
Лог = Логирование.ПолучитьЛог("duckdns.lib.updaterDDNS");
ИндексРезультата = 0;
ИндексIP = 1;
ИндексИзменения = 3;
#КонецОбласти